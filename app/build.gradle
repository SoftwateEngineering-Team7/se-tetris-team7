
plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

sourceSets {
  main {
    resources {
        // 리소스 디렉터리 2곳을 모두 클래스패스에 포함
      srcDirs = ['src/main/resources', 'src/main/java']

      // java 폴더에서 .fxml/.css/.png만 포함하고 .java는 제외
      include '**/*.fxml', '**/*.css', '**/*.png'
      exclude '**/*.java'
    }
  }
}

dependencies {
    implementation libs.guava
    testImplementation libs.junit
    // 개별 JavaFX 의존성은 필요 없음 — 아래 javafx 블록이 처리
    // TestFX for JavaFX GUI testing
    testImplementation "org.testfx:testfx-core:4.0.18"
    testImplementation "org.testfx:testfx-junit:4.0.18"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'org.tetris.App'
}

javafx {
    version = '21'
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = true
        html.required = true
    }
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: [
        ])
    }))
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit { minimum = 0.6 }
        }
    }
}

test {
    useJUnit()
    ignoreFailures = true
}

tasks.register('packageApp', Exec) {
    dependsOn build
    group = 'distribution'
version = '1.0.0'

    description = 'Build executable package with jpackage'

    doFirst {
        println "📦 Packaging JavaFX app with jpackage..."
    }

    // OS별로 다르게 설정 가능
    def outputDir = "${buildDir}/dist"
    def iconFile = "src/main/resources/tetris.icns" // macOS라면 .icns, windows는 .ico 사용
    def mainJar = "${buildDir}/libs/${project.name}-${version}.jar"

    commandLine 'jpackage',
            '--name', 'Team7 Tetris',
            '--input', "${buildDir}/libs",
            '--main-jar', "${project.name}-${version}.jar",
            '--main-class', 'org.tetris.App',
            '--icon', iconFile,
            '--type', 'dmg',             // macOS는 'dmg' 또는 'pkg', Linux는 'deb' 또는 'rpm', Windows는 'exe' 또는 'msi'
            '--dest', outputDir,
            '--app-version', '1.0',
            '--vendor', 'MyStudio'
}